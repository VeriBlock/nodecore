plugins {
    id "java"
    id "idea"
    id "java-library"
    id "maven-publish"
    id "com.google.protobuf"
    id "com.jfrog.artifactory"
}

dependencies {
    compile project(':veriblock-core')
    compile group: 'io.grpc', name: 'grpc-netty-shaded', version: '1.25.0'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.25.0'
    compile group: 'io.grpc', name: 'grpc-stub', version: '1.25.0'
    compileOnly 'javax.annotation:javax.annotation-api:1.2'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.19.0'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {
                    outputSubDir = 'java'
                }
            }
        }
    }
}

sourceSets {
    main {
        proto {}
        java {
            srcDir "$projectDir/src/main/java"
            srcDir "$projectDir/src/generated/main/java"
        }
    }
}

jar {
    archiveName = "${project.name}-${prettyVersion()}.jar"
    manifest {
        attributes 'Name': 'nodecore/api/grpc',
                'Specification-Title': 'VeriBlock NodeCore gRPC API',
                'Specification-Version': prettyVersion(),
                'Specification-Vendor': 'VeriBlock Foundation',
                'Implementation-Title': 'nodecore.api.grpc',
                'Implementation-Version': prettyVersion(),
                'Implementation-Vendor': 'VeriBlock Foundation'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            repoKey = artifactory_repoKey
            username = artifactory_user
            password = artifactory_password
            maven = true
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }

    resolve {
        repoKey = 'jcenter'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'veriblock'
            artifactId 'nodecore-grpc'
            version prettyVersion()
            from components.java
            artifact (sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}

clean {
    delete 'src/generated'
}

publishToMavenLocal {
    doFirst {
        println "Publishing package to local maven repo..."
    }
}

apply plugin: 'jacoco'

jacocoTestReport {
    getAdditionalSourceDirs() from files(project.sourceSets.main.allJava.srcDirs)
    getSourceDirectories() from files(project.sourceSets.main.allSource.srcDirs)
    getClassDirectories() from files(project.sourceSets.main.output)

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
