plugins {
    id "java"
    id "idea"
    id "application"
    id "com.google.protobuf"
}

configurations.all {
    // check for updates every build for changing modules
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.+"

    compile "org.codehaus.groovy:groovy:2.4.12"

    compile project(':nodecore-ucp')
    compile project(':nodecore-grpc')
	
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'com.google.protobuf', name: 'protobuf-gradle-plugin', version: '0.8.6'
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.1.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'
    compile group: 'com.diogonunes', name: 'JCDP', version: '2.0.3.1'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.1'
    compile group: 'org.quartz-scheduler', name: 'quartz-jobs', version: '2.2.1'
    compile group: 'com.j256.ormlite', name: 'ormlite-core', version: '5.1'
    compile group: 'com.j256.ormlite', name: 'ormlite-jdbc', version: '5.1'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.23.1'
    compile group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.14.7'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.7.2'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }
}

sourceSets {
    main {
        proto {}
        java {
            srcDir "$projectDir/src/main/java"
            srcDir "$projectDir/src/generated/main/java"
        }
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

run {
    standardInput = System.in
    standardOutput = System.out
    
    //if (System.getProperty('DEBUG', 'false') == 'true') {
    //    jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
    //}

    // example: -PappArgs="['-Dpeer.host=test.foo.com', '-Dpeer.port=6501']"
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

ext {
    group = 'veriblock'
    println "archiveBaseName: $archivesBaseName"
    println "branch = ${release.grgit.branch.current.name}"
}

jar {
    manifest {
        attributes 'Name': 'nodecore/miners/pop',
                'Specification-Title': 'VeriBlock Proof-of-Proof (PoP) Miner',
                'Specification-Version': prettyVersion(),
                'Specification-Vendor': 'VeriBlock Foundation',
                'Implementation-Title': 'nodecore.miners.pop',
                'Implementation-Version': version,
                'Implementation-Vendor': 'VeriBlock Foundation',
                'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
}

applicationName = 'nodecore-pop'
mainClassName = 'nodecore.miners.pop.Program'

startScripts {
    dependsOn jar

    classpath = files(jar.archivePath)
    windowsStartScriptGenerator.template = resources.text.fromFile("windowsStartScript.txt")
}
