// VeriBlock Blockchain Project
// Copyright 2017-2018 VeriBlock, Inc
// Copyright 2018-2019 Xenios SEZC
// All rights reserved.
// https://www.veriblock.org
// Distributed under the MIT software license, see the accompanying
// file LICENSE or http://www.opensource.org/licenses/mit-license.php.

plugins {
    id "java"
    id "idea"
    id "java-library"
    id 'org.jetbrains.kotlin.jvm'
    id "com.jfrog.artifactory"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "4.0.0"
}

configurations.all {
    // check for updates every build for changing modules
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation project(":veriblock-core")
    implementation project(":altchain-sdk")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    implementation "com.github.kittinunf.fuel:fuel:2.2.0"

    // Dependency Injection
    implementation "org.koin:koin-core:2.0.1"

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.6.26'

    testImplementation 'junit:junit:4.12'
    testImplementation 'io.kotlintest:kotlintest-assertions:3.4.2'
    testImplementation "io.mockk:mockk:1.9.3"
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    archiveName = "${project.name}-${prettyVersion()}.jar"
    manifest {
        attributes 'Name': 'veriblock/alt/plugins',
                'Specification-Title': 'VeriBlock Altchain Plugins',
                'Specification-Version': prettyVersion(),
                'Specification-Vendor': 'VeriBlock Foundation',
                'Implementation-Title': 'org.veriblock.alt.plugins',
                'Implementation-Version': prettyVersion(),
                'Implementation-Vendor': 'VeriBlock Foundation'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            repoKey = artifactory_repoKey
            username = artifactory_user
            password = artifactory_password
            maven = true
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'veriblock'
            artifactId = 'altchain-plugins'
            version = prettyVersion()

            from components.java
        }
    }
}

apply plugin: 'jacoco'

jacocoTestReport {
    getAdditionalSourceDirs() from files(project.sourceSets.main.allJava.srcDirs)
    getSourceDirectories() from files(project.sourceSets.main.allSource.srcDirs)
    getClassDirectories() from files(project.sourceSets.main.output)

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
