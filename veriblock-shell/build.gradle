
// VeriBlock Blockchain Project
// Copyright 2017-2018 VeriBlock, Inc
// Copyright 2018-2019 Xenios SEZC
// All rights reserved.
// https://www.veriblock.org
// Distributed under the MIT software license, see the accompanying
// file LICENSE or http://www.opensource.org/licenses/mit-license.php.

plugins {
    id "java"
    id "idea"
    id "java-library"
    id 'org.jetbrains.kotlin.jvm'
    id 'nebula.release'
    id "com.jfrog.artifactory"
    id "maven-publish"
}

dependencies {
    implementation project(":veriblock-core")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // Json serialization
    implementation 'com.google.code.gson:gson:2.8.2'

    compile 'ch.qos.logback:logback-classic:1.2.3'

    compile group: 'com.diogonunes', name: 'JCDP', version: '2.0.3.1'

    compile group: 'org.jline', name: 'jline', version: '3.13.1'
    compile group: 'org.jline', name: 'jline-terminal', version: '3.13.1'
    compile group: 'org.jline', name: 'jline-terminal-jansi', version: '3.13.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.0'
}

jar {
    archiveName = "${project.name}-${prettyVersion()}.jar"
    manifest {
        attributes 'Name': 'org/veriblock/shell',
                'Specification-Title': 'VeriBlock Shell',
                'Specification-Version': prettyVersion(),
                'Specification-Vendor': 'Xenios SEZC',
                'Implementation-Title': 'org.veriblock.shell',
                'Implementation-Version': prettyVersion(),
                'Implementation-Vendor': 'Xenios SEZC'
    }
}

artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            repoKey = artifactory_repoKey
            username = artifactory_user
            password = artifactory_password
            maven = true
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'veriblock'
            artifactId = 'shell'
            version = this.version

            from components.java
        }
    }
}

apply plugin: 'jacoco'

jacocoTestReport {
    getAdditionalSourceDirs() from files(project.sourceSets.main.allJava.srcDirs)
    getSourceDirectories() from files(project.sourceSets.main.allSource.srcDirs)
    getClassDirectories() from files(project.sourceSets.main.output)

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
