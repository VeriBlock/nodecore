buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-release-plugin:6.0.0'
        classpath 'org.ajoberstar:grgit:1.1.0'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.3"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'nebula.release' version '6.0.0'
    id 'com.jfrog.artifactory' version '4.7.3'
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
    }
}

subprojects {
    version = version
}

nebulaRelease {
    addReleaseBranchPattern(/(release(-|\/))?\d+(.\d+)?(.\d+)?/)
}

releaseCheck.doLast {
    // Print version to configure TeamCity
    println "##teamcity[buildNumber \'${prettyVersion()}\']"
}

String prettyVersion() {
    String version = rootProject.version.toString()
    if (version.contains("+")) {
        if (version.contains("+develop")) {
            version = version.split("\\+")[0]
        } else {
            version = version.substring(0, version.lastIndexOf(".")).replace("+", ".")
        }
    }
    return version
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}
