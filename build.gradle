buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-release-plugin:6.0.0'
        classpath 'org.ajoberstar:grgit:1.1.0'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.3"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'nebula.release' version '6.0.0'
    id 'com.jfrog.artifactory' version '4.7.3'
    id 'jacoco'
    id "org.sonarqube" version "2.8"
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    version = version
}

nebulaRelease {
    addReleaseBranchPattern(/(release(-|\/))?\d+(.\d+)?(.\d+)?/)
}

releaseCheck.doLast {
    // Print version to configure TeamCity
    println "##teamcity[buildNumber \'${prettyVersion()}\']"
}

String prettyVersion() {
    String version = rootProject.version.toString()
    if (version.contains("+")) {
        version = version.substring(0, version.length() - 8).replace("+", ".")
        if (version.endsWith("develop")) {
            version = version.substring(0, version.length() - 8)
        }
    }
    return version
}

sonarqube {
    properties {
        property "sonar.projectKey", "vbk:nodecore"
        property "sonar.projectName", "NodeCore Application Suite"
        property "sonar.projectVersion", prettyVersion()
        property "sonar.host.url", "https://sonar.veriblock.org"
        property "sonar.java.binaries", "${project.projectDir}/build/classes"
        property "sonar.java.libraries", "${project.projectDir}/build/libs"
        property "sonar.java.test.binaries", "${project.projectDir}/build/test-results/test/binary"
        property "sonar.junit.reportsPaths", "${project.projectDir}/build/test-results/**/*.xml"
        property "sonar.jacoco.reportPaths", "${project.projectDir}/build/jacoco/test.exec"
        property "sonar.exclusions", "**/generated/*.java"
    }
}

jacoco {
    toolVersion = '0.8.5'
}

wrapper {
    gradleVersion = '5.6.4'
}
