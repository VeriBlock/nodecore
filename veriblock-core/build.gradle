plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm"
    id "idea"
    id "java-library"
    id "maven-publish"
    id "com.jfrog.artifactory"
}

dependencies {
    // Kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    runtimeOnly "org.jetbrains.kotlin:kotlin-reflect"

    // Json serialization
    api 'com.google.code.gson:gson:2.8.5'

    // Configuration
    api "io.github.config4k:config4k:0.4.0"

    // Logging
    api 'ch.qos.logback:logback-classic:1.2.3'
    api 'io.github.microutils:kotlin-logging:1.6.26'

    // Joda Time
    api 'joda-time:joda-time:2.9.9'

    // Unit testing
    testImplementation 'junit:junit:4.12'
}

jar {
    archiveName = "veriblock-core-${prettyVersion()}.jar"
    manifest {
        attributes 'Name': 'org/veriblock/core',
                'Specification-Title': 'VeriBlock Core Library',
                'Specification-Version': prettyVersion(),
                'Specification-Vendor': 'VeriBlock Foundation',
                'Implementation-Title': 'org.veriblock.core',
                'Implementation-Version': prettyVersion(),
                'Implementation-Vendor': 'VeriBlock Foundation'
    }
    // Include source code
    from sourceSets.main.allSource
}


artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            repoKey = artifactory_repoKey
            username = artifactory_user
            password = artifactory_password
            maven = true
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'veriblock'
            artifactId 'veriblock-core'
            version prettyVersion()
            from components.java
        }
    }
}

apply plugin: 'jacoco'

jacocoTestReport {
    getAdditionalSourceDirs() from files(project.sourceSets.main.allJava.srcDirs)
    getSourceDirectories() from files(project.sourceSets.main.allSource.srcDirs)
    getClassDirectories() from files(project.sourceSets.main.output)

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
